TITLE DIV16
	.z80
	cseg
	ENTRY	DIV16
DIV16:
;
;	Utility integer divide.
;
;	Returns:
;
;		HL = HL/DE
;		DE = MOD(HL/DE)
;
	PUSH	IX		;SET UP THE WORKING REGISTERS
	PUSH	HL		;  BC IS QUOTIENT
	POP	IX		;  DE IS DIVISOR
	LD	BC,0		;  HL_IX ARE THE DIVIDEND
	LD	HL,0
	LD	A,16		;A IS THE LOOP COUNTER
;
DIV1:
	ADD	IX,IX		;SHIFT THE NEXT BIT OF IX 
	RL	L		;INTO HL
	RL	H
;
	OR	A		;CLEAR THE CARRY BIT
	SBC	HL,DE		;IF THE DIVISOR IS LARGER THAN THE QUOTIENT
	JP	C,DIV2		;THIS POSITION IN THE QUOTIENT GETS A ZERO
;
	SET	0,C		;ELSE IT GETS A 1
	JP	DIV3
DIV2:
	ADD	HL,DE		;RESTORE HL
DIV3:
	DEC	A		;TEST FOR COMPLETION
	JP	Z,DIV4		;GO BACK IF NOT DONE
;
	OR	A		;CLEAR THE CARRY BIT
	RL	C		; SHIFT BC LEFT 1 BIT
	RL	B
;
	JP	DIV1
;
DIV4:
	EX	DE,HL		;PUT THE REMAINDER IN DE
	LD	L,C		;PUT THE QUOTIENT IN HL
	LD	H,B
;
	POP	IX		;RESTORE IX
	RET
	END
